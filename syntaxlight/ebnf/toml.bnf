tomlFile ::= expression *

expression ::= key_value | table | table_array

table ::= table_header table_entries
table_header ::= '[' path ']'

table_array ::= table_array_header table_entries
table_array_header ::= '[' '[' path ']' ']'

key_or_str ::= key | string
path ::= key_or_str ('.' key_or_str) *
table_entries ::= key_value *

key_value ::= key_or_str '=' value

//TODO inline_table should be on the single line
inline_table ::= '{' (key_value (',' key_value)* ','?)? '}'

value ::= string | number | boolean | date | array | inline_table
array ::= '[' (value (',' value)* ','?)? ']'